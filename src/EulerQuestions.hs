{-# LANGUAGE NumericUnderscores #-}
{-# OPTIONS_GHC -Wno-missing-export-lists #-}

module EulerQuestions where

import Data.Foldable (Foldable (foldl'))
import Data.Function
import qualified Data.Map.Strict as Map
import Data.Maybe (fromMaybe)
import Data.Monoid
import qualified Data.Sequence as Seq
import Debug.Trace

-- ANCHOR Q1
-- If we list all the natural numbers below 10 that are multiples of 3 or 5, we get 3, 5, 6 and 9. The sum of these multiples is 23.
-- Find the sum of all the multiples of 3 or 5 below 1000.
isMultipleOf :: Int -> Int -> Bool
x `isMultipleOf` y = x `rem` y == 0

-- Input 1000 ->
q1 :: Int
q1 =
  sum
    (filter (\x -> x `isMultipleOf` 3 || x `isMultipleOf` 5) [0 .. (1_000 - 1)])

-- ANCHOR Q2
-- Each new term in the Fibonacci sequence is generated by adding the previous two terms.
-- By starting with 1 and 2, the first 10 terms will be:
-- 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
-- By considering the terms in the Fibonacci sequence whose values do not exceed four million,
-- find the sum of the even-valued terms.
fibSeq :: [Int]
-- Elegant recursive solution; fibSeq if f(n - 2), and (tail fibSeq) is f(n - 1). This
-- is because we are defining the recursive part starting at the 3rd element
fibSeq = 1 : 2 : zipWith (+) fibSeq (tail fibSeq)

q2 :: Int
q2 =
  fibSeq
    & takeWhile (<= 4_000_000)
    & filter even
    & sum

-- ANCHOR Q3
-- The prime factors of 13195 are 5, 7, 13 and 29.
-- What is the largest prime factor of the number 600851475143 ?
type Factor = Int

type Frequency = Int

intSqrRt :: Int -> Int
intSqrRt = round . sqrt . (fromIntegral :: Int -> Float)

updateCounterMap :: Ord k => k -> Map.Map k Frequency -> Map.Map k Frequency
updateCounterMap k = Map.insertWith (+) k 1

primeFactors :: Int -> Map.Map Factor Frequency
primeFactors n =
  primeFactors'
    n
    -- List of numbers of 2 and odd numbers less than sqrt(n)
    (2 : takeWhile (<= intSqrRt n) [3, 5 ..])
    Map.empty
  where
    -- Check if f can divide n' evenly. If so, update its number in a frequency map,
    -- perform n' = f / n', and try dividing f into the new n'.
    -- If you can't divide evenly, try with the next number in fs.
    -- If you exhaust the list
    primeFactors' n' [] freqMap = if n' > 2 then updateCounterMap n' freqMap else freqMap
    primeFactors' n' (f : fs) freqMap =
      if n' < 2
        then freqMap
        else
          let (d, m) = n' `divMod` f
           in if m == 0
                then primeFactors' d (f : fs) (updateCounterMap f freqMap)
                else primeFactors' n' fs freqMap

q3 :: Factor
q3 = fst $ fromMaybe (0, 0) $ Map.lookupMax $ primeFactors 600_851_475_143

-- ANCHOR Q4
-- A palindromic number reads the same both ways.
-- The largest palindrome made from the product of two 2-digit numbers is 9009 = 91 Ã— 99.
-- Find the largest palindrome made from the product of two 3-digit numbers.
-- Note: Only works for positive numbers
reverseNumber :: Int -> Int
reverseNumber = reverseNumber' 0
  where
    reverseNumber' acc 0 = acc
    reverseNumber' acc x =
      let (d, m) = x `divMod` 10 in reverseNumber' (acc * 10 + m) d

isNumPalendromic :: Int -> Bool
isNumPalendromic n = n == reverseNumber n

-- Originally used reverseNumber to avoid converting to Lists and then reversing the list, but
-- somehow converting to list -> reversing list was faster!!
q4 :: Int
q4 = maximum [x * y | x <- [100 .. 999], y <- [x .. 999], show (x * y) == reverse (show (x * y))]

-- ANCHOR Q5
-- 2520 is the smallest number that can be divided by each of the numbers from 1 to 10 without any remainder.
-- What is the smallest positive number that is evenly divisible by all of the numbers from 1 to 20?

-- Algorithm: Each number can be written as a product of its prime factors.
--            For numbers 1 .. n, create a frequency map of their prime factors;
--            E.g., 8 = {2:3} (Prime factor 2 appears 3 times), 6 = {2:1, 3:1}, 5 = {5:1} (Note that we ignore '1' as a factor)
--            Keep track of a master frequency map, keeping track of the **maximum** number of times a prime factor appears
--            At the end, multiply each prime factor by their maximum occurence to find the answer.
q5 :: Int
q5 =
  [2 .. 20]
    -- Prime factors of each number in list
    & fmap primeFactors
    -- Combining the prime factors for each number, keeping the largest frequency of each number
    & foldl' (Map.unionWith max) Map.empty
    -- Multiplying Factor ^ Frequency
    & Map.foldlWithKey' (\acc factor frequency -> acc * factor ^ frequency) 1

-- ANCHOR Q6
-- Find the difference between the sum of the squares of the first
-- one hundred natural numbers and the square of the sum.
q6 :: Int
q6 = sum [1 .. 100] ^ (2 :: Int) - sum [x ^ (2 :: Int) | x <- [1 .. 100]]

-- ANCHOR Q7
-- By listing the first six prime numbers: 2, 3, 5, 7, 11, and 13,
-- we can see that the 6th prime is 13.
-- What is the 10 001st prime number?

-- Keep a list of prime numbers found so far
-- Iterate through a list of 2 + all odd numbers, checking if
-- each number can be divided by any number in the list. If not, add it to the set.
nthPrime :: Int -> Int
nthPrime n = case Seq.viewr $ calculatePrimeSeq n (2 : [3, 5 ..]) Seq.empty of
  Seq.EmptyR -> error "There should always be at least one prime"
  _ Seq.:> lastElement -> lastElement
  where
    -- Produces a Seq of the first n primes
    -- 1st arg is how many primes to calculate
    -- 2nd arg is a ordered list of numbers to filter primes from
    -- 3rd arg is the accumulated primes found so far
    -- Use Seq for fast append/prepends
    calculatePrimeSeq :: Int -> [Int] -> Seq.Seq Int -> Seq.Seq Int
    calculatePrimeSeq 0 _ primes = primes
    calculatePrimeSeq _ [] primes = primes
    calculatePrimeSeq n' (numToCheck : ns) primes =
      -- We check if numToCheck is prime by checking if
      -- any of the numbers inside primes (less than sqrt(numToCheck))
      -- divide it evenly. If not, then it is prime
      let primesToCheck = Seq.takeWhileL (<= intSqrRt numToCheck) primes
          isPrime =
            -- primesToCheck could be empty if sqrt(numToCheck) < anything in primes
            -- In this case, numToCheck is indeed prime
            Seq.null primesToCheck
              -- If numToCheck is a multiple of any numbers inside primesToCheck, then it is not prime.
              || not (getAny (foldMap (Any . (numToCheck `isMultipleOf`)) primesToCheck))
       in if isPrime
            then calculatePrimeSeq (n' - 1) ns (primes Seq.|> numToCheck)
            else calculatePrimeSeq n' ns primes

q7 :: Int
q7 = nthPrime 10_001